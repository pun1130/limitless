import com.modrinth.minotaur.TaskModrinthUpload

buildscript {
    configurations.classpath {
        resolutionStrategy.eachDependency {
            if (requested.name == "fabric-loom") {
                useVersion("0.9.+")
            }
        }
    }
}

plugins {
    id("wheel-fabric").version("latest.integration")
    id("kbootstrap").version("latest.integration")
    id("fabric.mod.gradle").version("latest.integration")
    id("com.modrinth.minotaur").version("latest.integration")
}

group = "net.auoeke"
version = "1.5.7"
description = "a mod about enchanting and exceeding Minecraft's limits"

defaultTasks("generateMetadata")

sourceSets {
    main {
        kotlin.srcDir("src")
        java.srcDir("src")

        resources {
            srcDir("resources")
            include("assets/", "data/", "*.json")
        }
    }

    test {
        java.srcDir("test")
    }
}

repositories {
    maven {url = "https://maven.auoeke.net"}
}

dependencies {
    include(
        mod("api-base"),
        mod("api-resource-loader"),
        mod("api-tag-extensions"),
        mod("net.fabricmc.fabric-api:fabric-command-api-v1:latest.integration"),
        mod("net.auoeke:huntingham-hills:latest.integration"),
        mod("net.auoeke:reflect"),
        bloated("cloth-config"),
        api("net.auoeke:extensions"),
        api("net.auoeke:asm-extensions")
    )

    bloated("mod-menu:latest.integration")
}

configurations.all {
    resolutionStrategy.eachDependency {
        if (requested.name == "fabric-resource-loader-v0") {
            useVersion("0.4.8+a00e834b18")
        }
    }
}

mod {
    author("auoeke")

    contact {
        sources = "https://git.auoeke.net/limitless"
        issues = "https://git.auoeke.net/limitless/issues"
    }

    license("GNU LGPL v3.0")
    icon("assets/limitless/icon.png")
    mixin("limitless.mixins.json")

    entrypoints(
        main: [kbootstrap: 'net.auoeke.limitless.Limitless'],
        client: [kbootstrap: 'net.auoeke.limitless.Limitless'],
        modmenu: [kbootstrap: 'net.auoeke.limitless.Limitless$ModMenu']
    )
}

task modrinth(type: TaskModrinthUpload) {
    if (token = System.properties.modrinth) {
        dependsOn(publish.finalizedBy(it))
    }

    projectId = "Qx0jUYY8"
    versionNumber = version
    uploadFile = remapJar

    addGameVersion("1.17.1")
    addLoader("fabric")

    doFirst {
        logger.lifecycle(/Enter the changelog ending with "EOF"./)
        changelog = ""

        for (def reader = System.in.newReader(), line; true; changelog += line + System.lineSeparator()) {
            line = reader.readLine()

            if (line.endsWith("EOF")) {
                if (line.length() > 3) {
                    changelog += line.replaceFirst(/EOF$/, "")
                }

                break
            }
        }
    }
}
